<?xml version="1.0" encoding="utf-8"?>

<!--
/*
* The contents of this file are subject to the Common Public Attribution License

* Version 1.0 (the "License"); you may not use this file except in compliance
* with the License. You may obtain a copy of the License at http://cpal.abiquo.com.



* The License is based on the Mozilla Public License Version 1.1 but Sections 14
* and 15 have been added to cover use of software over a computer network and
* provide for limited attribution for the Original Developer. In addition,



* Exhibit A has been modified to be consistent with Exhibit B.
*
* Software distributed under the License is distributed on an "AS IS" basis,
* WITHOUT WARRANTY OF ANY KIND, either express or implied. See the License



* for the specific language governing rights and limitations under the License.
*
* The Original Code is abiquo,  14 Jul 2008. The Original Developer is the
* Initial Developer. The Initial Developer of the Original Code is Soluciones Grid,



* S.L.. All portions of the code are Copyright Â© Soluciones Grid, S.L.
* All Rights Reserved.
*/

-->
<AbiCloudPopUp xmlns="net.undf.abicloud.view.common.*" xmlns:mx="http://www.adobe.com/2006/mxml"
			   creationComplete="onCreationComplete()"
			   titleText="@Resource(bundle='Infrastructure', key='POPUP_TITLE_PHYSICALMACHINEADD')">

	<mx:Metadata>
		[Event(name="physicalMachineAddClose", type="flash.events.Event")]
	</mx:Metadata>
	
	<mx:Script>
		<![CDATA[
			import mx.resources.ResourceBundle;
			import mx.resources.ResourceManager;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import net.undf.abicloud.view.common.AbiCloudAlert;
			import net.undf.abicloud.vo.infrastructure.PhysicalMachineCreation;
			import net.undf.abicloud.vo.infrastructure.HyperVisor;
			import mx.managers.PopUpManager;
			import net.undf.abicloud.model.AbiCloudModel;
			import net.undf.abicloud.vo.infrastructure.SO;
			import net.undf.abicloud.vo.infrastructure.NetworkModule;
			import net.undf.abicloud.vo.infrastructure.DNS;
			import mx.collections.ArrayCollection;
			import net.undf.abicloud.vo.infrastructure.PhysicalMachine;
			import net.undf.abicloud.events.InfrastructureEvent;
			import net.undf.abicloud.vo.infrastructure.Rack;
			import net.undf.abicloud.vo.infrastructure.DataCenter;
			
			[ResourceBundle("Common")]
			private var rb:ResourceBundle;
			[ResourceBundle("Infrastructure")]
			private var rb2:ResourceBundle;
			
			//The physical machine that will be added
			private var _physicalMachine:PhysicalMachine;
			
			//The DataCenter where the new Physical Machine will be stored
			private var _dataCenter:DataCenter;
			
			//Array with the network modules that the new physical machine will have
			[Bindable]
			private var _networkModules:ArrayCollection;
			
			[Bindable]
			private var _hypervisors:ArrayCollection;
			
			//Pop Up to create a new Network Module
			private var _networkModuleForm:NetworkModuleForm;
			
			//Pop Up to create an Hypervisor
			private var _hypervisorForm:HypervisorForm;
			
			private function onCreationComplete():void
			{
				//Creating pop up to be able to add new network modules
				_networkModuleForm = new NetworkModuleForm();
				_networkModuleForm.addEventListener("networkModuleFormAccept", onNetworkModuleFormAccept);
				_networkModuleForm.addEventListener("networkModuleFormCancel", onNetworkModuleFormCancel);	
				
				//Creating pop up to be able to add new hypervisors
				_hypervisorForm = new HypervisorForm();
				_hypervisorForm.addEventListener("hypervisorFormAccept", onHypervisorFormAccept);
				_hypervisorForm.addEventListener("hypervisorFormCancel", onHypervisorFormCancel);
			}
			
			/**
			 * Initializes this component
			 **/
			public function init(dataCenter:DataCenter):void
			{
				this._dataCenter = dataCenter;
				this._physicalMachine = new PhysicalMachine();
				
				//Cleaning the form
				nameInput.text = "";
				rackInput.selectedIndex = -1;
				descriptionInput.text = "";
				ramInput.value = ramInput.minimum;
				cpuInput.value = cpuInput.minimum;
				hdInput.value = hdInput.minimum;
				soInput.selectedIndex = -1;
				_networkModules = new ArrayCollection();
				_hypervisors = new ArrayCollection();
				
				acceptButton.enabled = false;
				nameInput.setFocus();
				nameInput.setFocus();
			}
			
			
			 
			/**
			 * When user wants to create a new Network Module
			 **/
			 private function onClickAddNetworkModule():void
			 {
			 	PopUpManager.addPopUp(_networkModuleForm, this, true);
			 	PopUpManager.centerPopUp(_networkModuleForm);
			 	_networkModuleForm.networkModule = null;
			 	
			 }
			 
			 /**
			 * When user clicks to delete a created network module
			 **/
			 private function onClickDeleteNetworkModule():void
			 {
			 	//First, we check if the network module's ip is used by an Hypervisor
			 	var networkModule:NetworkModule = networkModuleList.selectedItem as NetworkModule;
			 	
			 	if(! networkModule.dhcp)
				{
					var length:int = this._hypervisors.length;
					var i:int;
					for(i = 0; i < length; i++)
					{
						if(HyperVisor(this._hypervisors.getItemAt(i)).ip == networkModule.ip)
						{
							AbiCloudAlert.showAlert(ResourceManager.getInstance().getString("Common", "ALERT_TITLE_LABEL"),
									 ResourceManager.getInstance().getString("Infrastructure", "ALERT_DELETE_NETWORKMODULE_HEADER"),
									 ResourceManager.getInstance().getString("Infrastructure", "ALERT_DELETE_NETWORKMODULE_TEXT"),
									 Alert.YES | Alert.NO,
									 onDeleteNetworkModuleUserResponse);
									 
							return;
						}
					}
				}
				
				//This network module is not being used by any hypervsor
				deleteNetworkModule();
			 }
			 
			private function onDeleteNetworkModuleUserResponse(event:CloseEvent):void
			{
				if(event.detail == Alert.YES)
					deleteNetworkModule();
			}
			
			private function deleteNetworkModule():void
			{
				var index:int = _networkModules.getItemIndex(networkModuleList.selectedItem);
			 	_networkModules.removeItemAt(index);
			}
			 
			 /**
			 * When user clicks to edit a network module
			 **/
			 private function onClickEditNetworkModule():void
			 {
			 	PopUpManager.addPopUp(_networkModuleForm, this, true);
			 	PopUpManager.centerPopUp(_networkModuleForm);
			 	_networkModuleForm.networkModule = networkModuleList.selectedItem as NetworkModule;
			 }
			 
			 private function onNetworkModuleFormAccept(event:Event):void
			 {
			 	if(!_networkModules.contains(_networkModuleForm.networkModule))
			 		_networkModules.addItem(_networkModuleForm.networkModule);
			 		
			 	PopUpManager.removePopUp(_networkModuleForm);
			 }
			 
			 private function onNetworkModuleFormCancel(event:Event):void
			 {
			 	PopUpManager.removePopUp(_networkModuleForm);
			 }
			
			
			/**
			 * When user clicks to add a new hypervisor
			 */
			private function onClickAddHypervisor():void
			{
				PopUpManager.addPopUp(this._hypervisorForm, this, true);
				PopUpManager.centerPopUp(this._hypervisorForm);
				
				this._hypervisorForm.init(null, this._networkModules);
			}
			
			/**
			 * When user clicks to delete an Hypervisor
			 */
			private function onClickDeleteHypervisor():void
			{
				var index:int = this._hypervisors.getItemIndex(hypervisorList.selectedItem);
				this._hypervisors.removeItemAt(index);
			}
			
			/**
			 * When user clicks to edit an Hypervisor
			 */
			private function onClickEditHypervisor():void
			{
				PopUpManager.addPopUp(this._hypervisorForm, this, true);
				PopUpManager.centerPopUp(this._hypervisorForm);
				
				this._hypervisorForm.init(hypervisorList.selectedItem as HyperVisor, this._networkModules);
			}
			
			private function onHypervisorFormAccept(event:Event):void
			{
				var hypervisor:HyperVisor = this._hypervisorForm.hypervisor;
				hypervisor.assignedTo = this._physicalMachine;
				
				if(! this._hypervisors.contains(hypervisor))
					this._hypervisors.addItem(hypervisor);
					
				PopUpManager.removePopUp(this._hypervisorForm);
			}
			
			private function onHypervisorFormCancel(event:Event):void
			{
				PopUpManager.removePopUp(this._hypervisorForm);	
			}
			
			
			/**
			 * When user clicks Accept button, we save the new physical machine
			 **/
			 private function onAccept():void
			 {
			 	//Saving the new physical machine's attributes
			 	_physicalMachine.name = nameInput.text;
			 	_physicalMachine.assignedTo = rackInput.selectedItem as Rack;
			 	_physicalMachine.dataCenter = this._dataCenter;
			 	_physicalMachine.description = descriptionInput.text;
			 	_physicalMachine.ram = ramInput.value;
			 	_physicalMachine.cpu = cpuInput.value;
			 	_physicalMachine.hd = hdInput.value * AbiCloudModel.GB_TO_BYTES; //HD is sent in bytes, instead of GB
			 	_physicalMachine.hostSO = soInput.selectedItem as SO;
			 	_physicalMachine.networkModuleList = _networkModules;
			 	
			 	//Announcing that user wants to create a new Physical Machine, along with hypervisors
			 	var event:InfrastructureEvent = new InfrastructureEvent(InfrastructureEvent.CREATE_PHYSICALMACHINE);
			 	var physicalMachineCreation:PhysicalMachineCreation = new PhysicalMachineCreation();
			 	physicalMachineCreation.physicalMachine = this._physicalMachine;
			 	physicalMachineCreation.hypervisors = this._hypervisors;
			 	event.physicalMachineCreation = physicalMachineCreation;
			 	dispatchEvent(event);
			 	
			 	//Closing the form
			 	dispatchEvent(new Event("physicalMachineAddClose"));
			 }
			 
			 /**
			 * Cancels Physical Machine creation
			 **/
			 private function onCancel():void
			 {
			 	//Closing the form
				dispatchEvent(new Event("physicalMachineAddClose"));
			 }
			 
			 /**
			 * Activates the Accept Button if the form is valid
			 **/
			 private function validateForm():void
			 {
			 	if(rackInput.selectedIndex > -1
			 	   && soInput.selectedIndex > -1
			 	   && nameInput.text.length > 0)
			 	    acceptButton.enabled = true;
			 	   
			 	else
			 		acceptButton.enabled = false;
			 }
			 
			 private function networkModuleListLabelFunction(item:Object):String
			 {
			 	var networkModule:NetworkModule = item as NetworkModule;
			 	if(networkModule.ip.length > 0)
			 		return networkModule.ip
			 	else
			 		return "DHCP";
			 }
			 
			 /**
			 * Basic behaivour is as follows:
			 * If user presses the Enter, in any part of this Pop Up (except buttons), it would be
			 * like Accept Button has been pressed
			 */
			override protected function keyboardHandler(keyboardEvent:KeyboardEvent):void
			{
				if(keyboardEvent.target == acceptButton || keyboardEvent.target == cancelButton)
					super.keyboardHandler(keyboardEvent);
				else
				{
					if(keyboardEvent.keyCode == 13 && acceptButton.enabled)
						onAccept();
				}
			}
		]]>
	</mx:Script>
	
	<mx:HBox horizontalGap="2">
		<mx:Form paddingTop="3">
			<mx:FormItem label="@Resource(bundle='Common', key='TEXTINPUT_LABEL_NAME')" required="true">
				<mx:TextInput id="nameInput" maxChars="29"
							  change="validateForm()" height="20"/>
			</mx:FormItem>
			
			<mx:FormItem label="@Resource(bundle='Infrastructure', key='TEXTINPUT_LABEL_DESCRIPTION')">
				<mx:TextArea id="descriptionInput" maxChars="99"/>
			</mx:FormItem>
			
			<mx:FormItem label="@Resource(bundle='Infrastructure', key='LABEL_RACK')" required="true">
				<mx:ComboBox id="rackInput"
							 labelField="name"
							 dataProvider="{AbiCloudModel.getInstance().infrastructureManager.racks}"
							 prompt="@Resource(bundle='Infrastructure', key='LABEL_RACK')"
							 change="validateForm()" height="18"/>
			</mx:FormItem>
			
			<mx:FormItem label="@Resource(bundle='Infrastructure', key='COMBOBOX_LABEL_SO')" required="true">
				<mx:ComboBox id="soInput" 
							 labelField="description"
							 dataProvider="{AbiCloudModel.getInstance().operatingSystems}"
							 prompt="@Resource(bundle='Infrastructure', key='COMBOBOX_LABEL_SO')"
							 change="validateForm()" height="18"/>
			</mx:FormItem>
			
			
			<mx:FormItem id="networkModuleFormItem"
					 label="@Resource(bundle='Infrastructure', key='FORM_LABEL_NETWORKMODULES')"
					 direction="horizontal">
				
				<mx:VBox verticalGap="0">
					<mx:List id="networkModuleList" width="150" height="100"
							 dataProvider="{_networkModules}"
							 labelFunction="networkModuleListLabelFunction"/>
					<mx:Spacer width="6" height="6"/>
					<mx:HBox horizontalGap="3" width="100%" verticalAlign="middle">
						<mx:Button id="addNetworkModuleButton"
							       click="onClickAddNetworkModule()" styleName="bAgregar"
							       toolTip="@Resource(bundle='Common', key='BUTTON_NEW_TOOLTIP')"/>
							       
						<mx:Button id="deleteNetworkModuleButton"
							       click="onClickDeleteNetworkModule()"
							       enabled="{networkModuleList.selectedIndex > -1}" styleName="bNoAgregar"
							       toolTip="@Resource(bundle='Common', key='BUTTON_DEL_TOOLTIP')"/>
						<mx:Spacer width="100%" height="6"/>
							       
						<mx:Button id="editButton"
							   click="onClickEditNetworkModule()"
							   enabled="{networkModuleList.selectedIndex > -1}" styleName="bConfig"
							   toolTip="@Resource(bundle='Common', key='BUTTON_EDIT_TOOLTIP')"/>
					</mx:HBox>		 
					
				</mx:VBox>
				
			</mx:FormItem>
			
			<mx:FormItem direction="horizontal">
				<mx:Button id="acceptButton" label="@Resource(bundle='Common', key='BUTTON_ACCEPT')" 
						   click="onAccept()" height="18"/>
				
				<mx:Button id="cancelButton" label="@Resource(bundle='Common', key='BUTTON_CANCEL')"
						   click="onCancel()"  height="18" styleName="buttonGris"/>
			</mx:FormItem>
		</mx:Form>
		
		<mx:Form paddingTop="3">
			<mx:FormItem label="@Resource(bundle='Infrastructure', key='TEXTINPUT_LABEL_RAM')">
				<mx:NumericStepper id="ramInput" height="20" width="75"
								   minimum="0" maximum="32768" stepSize="256"/>
				
			</mx:FormItem>
			
			<mx:FormItem label="@Resource(bundle='Infrastructure', key='TEXTINPUT_LABEL_CPU')">
				<mx:NumericStepper id="cpuInput" height="20" width="75"
								   minimum="1" maximum="32" stepSize="1"/>
			</mx:FormItem>
			
			<mx:FormItem label="@Resource(bundle='Infrastructure', key='TEXTINPUT_LABEL_HD')">
				<mx:NumericStepper id="hdInput" height="20" width="75"
								   minimum="0" maximum="10000" stepSize="10"/>
			</mx:FormItem>
		
			<mx:Spacer height="41"/>
		
			<mx:FormItem 
					 label="@Resource(bundle='Infrastructure', key='FORM_LABEL_HYPERVISOR')"
					 direction="horizontal">
				
				<mx:VBox verticalGap="0">
					<mx:List id="hypervisorList"
							 width="170" height="100"
							 dataProvider="{this._hypervisors}"
							 labelField="shortDescription"/>
							 
					<mx:Spacer width="6" height="6"/>
					
					<mx:HBox horizontalGap="3" width="100%" verticalAlign="middle">
						<mx:Button id="addHypervisorButton"
								   enabled="{this._networkModules.length > 0}"
							       click="onClickAddHypervisor()" styleName="bAgregar"
							       toolTip="@Resource(bundle='Common', key='BUTTON_NEW_TOOLTIP')"/>
							       
						<mx:Button id="deleteHypervisorButton"
							       click="onClickDeleteHypervisor()"
							       enabled="{hypervisorList.selectedIndex > -1}" styleName="bNoAgregar"
							       toolTip="@Resource(bundle='Common', key='BUTTON_DEL_TOOLTIP')"/>
						<mx:Spacer width="100%" height="6"/>
							       
						<mx:Button id="editHypervisorButton"
							   click="onClickEditHypervisor()"
							   enabled="{hypervisorList.selectedIndex > -1}" styleName="bConfig"
							   toolTip="@Resource(bundle='Common', key='BUTTON_EDIT_TOOLTIP')"/>
					</mx:HBox>		 
					
				</mx:VBox>
				
			</mx:FormItem>
		</mx:Form>
	
	</mx:HBox>
	
</AbiCloudPopUp>

